# ESP8266 D1 Mini Relais als HomeKit Schalter ohne Bridge - Tutorial

Wenn es bisher darum ging den [ESP8266 D1 Mini](https://web.archive.org/web/20220627125251/https://makesmart.shop/loadBasket/fAK_Si9oESm) in HomeKit einzubinden, bin ich immer den Weg √ºber meine Homebridge auf dem Raspberry Pi gegangen. In der letzten Zeit hat sich da sehr viel getan und mittlerweile gibt es nicht nur eine L√∂sung das ganze auch ohne Bridge zu erreichen. Wir werden heute einen D1 Mini ohne Bridge oder sonstiges Zubeh√∂r direkt und nativ in HomeKit einbinden. Es handelt sich dabei erstmal nur um einen einfachen Schalter der ein Relaismodul bet√§tigt.

------

###  HOMEKIT-ESP8266 - ARDUINO IDE BIBLIOTHEK HINZUF√úGEN

Bevor wir damit beginnen unseren eigenen HomeKit-Schalter einzubinden, muss eine Bibliothek in der Arduino IDE installiert werden. Das geht √ºber den Bibliotheksverwalter. Ihr erreicht ihn √ºber das Men√º der Arduino IDE. Die Bibliothek hei√üt HomeKit-ESP8266 und kann √ºber die Suche des Bibliotheksverwalters gefunden werden.

```
Arduino IDE
‚îî‚îÄ‚îÄ‚îÄWerkzeuge
    ‚îî‚îÄ‚îÄ‚îÄBibliotheken verwalten...
```

[![HomeKit-ESP8266 - Arduino IDE Bibliothek installieren](https://web.archive.org/web/20220627125251im_/https://my.makesmart.net/assets/uploads/files/1598177591980-homekit-esp8266-bibliothek-installieren.png)](https://web.archive.org/web/20220627125251mp_/https://my.makesmart.net/assets/uploads/files/1598177591980-homekit-esp8266-bibliothek-installieren.png)

------

###  HOMEKIT-ESP8266 - DER CODE

Um den ESP8266 in HomeKit einzubinden, ist mehr als nur eine Datei notwendig. Ihr k√∂nnt dem Tutorial weiter folgen und die Dateien selber anlegen, oder die fertigen Dateien herunterladen: [makesmart-esp8266-homekit-relais.zip](https://web.archive.org/web/20220627125251/https://my.makesmart.net/assets/uploads/files/1598180988789-makesmart-esp8266-homekit-relais.zip)

------

√ñffnet die Arduino IDE und speichert das Projekt an einem beliebigen Ort. Nachdem das getan ist, m√ºsst ihr zwei weitere Dateien anlegen. Das √ºber die Tastenkombination `Strg + Umschalt + N` oder √ºber den Klick auf den Pfeil in der rechten oberen Ecke.

[![Arduino IDE neue Datei anlegen](https://web.archive.org/web/20220627125251im_/https://my.makesmart.net/assets/uploads/files/1598178175480-arduino_ide_neue_datei_anlegen.png)](https://web.archive.org/web/20220627125251mp_/https://my.makesmart.net/assets/uploads/files/1598178175480-arduino_ide_neue_datei_anlegen.png)

Nennt die erste Datei `my_accessory.c` und die zweite Datei `wifi_info.h`.

------

Jetzt kommen wir zum eigentlichen Code. Den Code k√∂nnt ihr 1:1 f√ºr die jeweilige Datei kopieren, einf√ºgen und speichern.

####  1. Datei: ***mein_schalter.ino***

```cpp
#include <Arduino.h>
#include <arduino_homekit_server.h>
#include "wifi_info.h"

#define LOG_D(fmt, ...)   printf_P(PSTR(fmt "\n") , ##__VA_ARGS__);

void setup() {
  Serial.begin(115200);
  wifi_connect();
  homekit_storage_reset();
  my_homekit_setup();
}

void loop() {
  my_homekit_loop();
  delay(10);
}

//==============================
// HomeKit setup and loop
//==============================

// Zugriff auf die Definitionen des Accessories in my_accessory.c
extern "C" homekit_server_config_t config;
extern "C" homekit_characteristic_t cha_switch_on;

static uint32_t next_heap_millis = 0;

#define PIN_SWITCH 15

// Diese Funktion wird aufgerufen, wenn der Schalter in HomeKit bet√§tigt wird
void cha_switch_on_setter(const homekit_value_t value) {
  bool on = value.bool_value;
  cha_switch_on.value.bool_value = on;
  LOG_D("Switch: %s", on ? "ON" : "OFF");
  digitalWrite(PIN_SWITCH, on ? HIGH : LOW);
}

void my_homekit_setup() {

  
  pinMode(PIN_SWITCH, OUTPUT);
  digitalWrite(PIN_SWITCH, HIGH);
  cha_switch_on.setter = cha_switch_on_setter;
  arduino_homekit_setup(&config);



  // Das hier kann verwendet werden, um den Status an HomeKit zur√ºckzusenden,
  // falls ein zus√§tzlicher pysischer Button o.√§ verwendet wird.
  
  // bool switch_is_on = true/false;
  // cha_switch_on.value.bool_value = switch_is_on;
  // homekit_characteristic_notify(&cha_switch_on, cha_switch_on.value);
}



void my_homekit_loop() {
  arduino_homekit_loop();
  const uint32_t t = millis();
  if (t > next_heap_millis) {
    // heap-Info alle 30 Sekunden im seriellen Monitor ausgeben
    next_heap_millis = t + 30 * 1000;
    LOG_D("Free heap: %d, HomeKit clients: %d",
        ESP.getFreeHeap(), arduino_homekit_connected_clients_count());

  }
}
```

####  2. Datei: ***my_accessory.c***

```cpp
#include <homekit/homekit.h>
#include <homekit/characteristics.h>

void my_accessory_identify(homekit_value_t _value) {
  printf("accessory identify\n");
}

// Switch (HAP section 8.38)
// required: ON
// optional: NAME

// format: bool; HAP section 9.70; write the .setter function to get the switch-event sent from iOS Home APP.
homekit_characteristic_t cha_switch_on = HOMEKIT_CHARACTERISTIC_(ON, true);

// max. L√§nge 64
homekit_characteristic_t cha_name = HOMEKIT_CHARACTERISTIC_(NAME, "Schalter");

homekit_accessory_t *accessories[] = {
    HOMEKIT_ACCESSORY(.id=1, .category=homekit_accessory_category_switch, .services=(homekit_service_t*[]) {
        HOMEKIT_SERVICE(ACCESSORY_INFORMATION, .characteristics=(homekit_characteristic_t*[]) {
            HOMEKIT_CHARACTERISTIC(NAME, "Schalter"),
            HOMEKIT_CHARACTERISTIC(MANUFACTURER, "makesmart Community"),
            HOMEKIT_CHARACTERISTIC(SERIAL_NUMBER, "1234567"),
            HOMEKIT_CHARACTERISTIC(MODEL, "ESP8266 D1 Mini"),
            HOMEKIT_CHARACTERISTIC(FIRMWARE_REVISION, "1.0"),
            HOMEKIT_CHARACTERISTIC(IDENTIFY, my_accessory_identify),
            NULL
        }),
    HOMEKIT_SERVICE(SWITCH, .primary=true, .characteristics=(homekit_characteristic_t*[]){
      &cha_switch_on,
      &cha_name,
      NULL
    }),
        NULL
    }),
    NULL
};

homekit_server_config_t config = {
    .accessories = accessories,
    .password = "123-45-678"
};
```

####  3. Datei: ***wifi_info.h***

Im folgenden Code m√ºsst ihr eure WLAN-Zugangsdaten anpassen. Es sind die folgenden beiden Variablen:

```cpp
const char *ssid = "WIFI-SSID";
const char *password = "WIFI-PASSWD";
#ifndef WIFI_INFO_H_
#define WIFI_INFO_H_

#if defined(ESP8266)
#include <ESP8266WiFi.h>
#elif defined(ESP32)
#include <WiFi.h>
#endif

const char *ssid = "WIFI-SSID";
const char *password = "WIFI-PASSWD";

void wifi_connect() {
  WiFi.persistent(false);
  WiFi.mode(WIFI_STA);
  WiFi.setAutoReconnect(true);
  WiFi.begin(ssid, password);
  Serial.println("WiFi connecting...");
  while (!WiFi.isConnected()) {
    delay(100);
    Serial.print(".");
  }
  Serial.print("\n");
  Serial.printf("WiFi connected, IP: %s\n", WiFi.localIP().toString().c_str());
}

#endif
```

Nachdem alle drei Dateien gespeichert wurden, kann das Programm auf den ESP8266 D1 Mini geladen werden. Geht dazu wieder in den Reiter ‚Äúmein_schalter‚Äù und ladet das Programm auf den ESP.

------

###  HOMEKIT-ESP8266 - DIE EINBINDUNG IN HOMEKIT

Die Einbindung des DIY ESP8266 Relais in HomeKit ist ziemlich einfach.√ñffnet dazu die Home-App und f√ºgt ein neues Ger√§t hinzu. Der Schalter sollte in den Ger√§ten in der N√§he auftauchen. Per Tippen auf den Schalter kann dieser hinzugef√ºgt werden. Zum Hinzuf√ºgen muss ein Code eingegeben werden, der Code zum Koppeln lautet `123-45-678`.

[![HomeKit neues Ger√§t hinzuf√ºgen](https://web.archive.org/web/20220627125251im_/https://my.makesmart.net/assets/uploads/files/1598180004543-esp8266_homekit_pairing.jpg)](https://web.archive.org/web/20220627125251mp_/https://my.makesmart.net/assets/uploads/files/1598180004543-esp8266_homekit_pairing.jpg)

------

###  HOMEKIT-ESP8266 - DIE VERDRAHTUNG

Der ESP wurde erfolgreich in HomeKit eingebunden und kann von nun an f√ºr Automationen und Szenen verwendet werden. Aber eins fehlt noch: Das Relais. Das ist wohl das geringste √úbel dieses Tutorials - hier der Schaltplan:

[![ESP8266 D1 Mini Relaiy KY-019 Schaltplan](https://web.archive.org/web/20220627125251im_/https://my.makesmart.net/assets/uploads/files/1598181409142-esp8266_homekit_relais_schaltplan.png)](https://web.archive.org/web/20220627125251mp_/https://my.makesmart.net/assets/uploads/files/1598181409142-esp8266_homekit_relais_schaltplan.png)

| KY-19 Relaismodul | ESP8266 D1 Mini |
| :---------------- | :-------------- |
| S                 | D8              |
| +                 | 5V              |
| -                 | G               |

------

Und fertig ist das HomeKit-Relais mit dem ESP8266 D1 Mini. Nativ. Ohne Bridge. Superschnell und super einfach. ![üôÇ](https://web.archive.org/web/20220627125251im_/https://my.makesmart.net/plugins/nodebb-plugin-emoji/emoji/apple/1f642.png?v=chnl3kn5hqg)

Erfolgreich getestet und im Einsatz hab ich es mit [diesen Produkten](https://web.archive.org/web/20220627125251/https://makesmart.shop/loadBasket/fAK_Si9oESm) vom [makesmart.shop](https://web.archive.org/web/20220627125251/http://makesmart.shop/).

------

Solltest du mit dem Programmieren / Debugging fertig sein, ist noch ein letzter Schritt n√∂tig.

```cpp
homekit_storage_reset();
```

Diese Zeile muss im `setup()` auskommentiert werden. Sonst muss der ESP8266 D1 Mini jedes mal aufs neue mit HomeKit gepaired werden. Wenn die Zeile auskommentiert oder entfernt ist, wird der D1 Mini auch nach dem Abstecken wieder als der gleiche Schalter erkannt und kann eingesetzt werden.

------

###  MODIFIKATIONEN AUS DER COMMUNITY

**Kann ich mehr als nur ein Relais steuern?**
[@Menuhin](https://web.archive.org/web/20220627125251/https://my.makesmart.net/uid/82) hat sich die Arbeit gemacht das Programm anzupassen
[Tutorial: Mehrere Relais an einem D1 Mini](https://web.archive.org/web/20220627125251/https://my.makesmart.net/topic/83/esp8266-d1-mini-relais-als-homekit-schalter-ohne-bridge-tutorial/15)

**Kann ich beim Bet√§tigen des Schalter in HK einen zus√§tzlichen GPIO kurz triggern lassen?**
Ja, [hier wird beim Bet√§tigen ein zus√§tzlicher GPIO getriggert](https://web.archive.org/web/20220627125251/https://my.makesmart.net/topic/83/esp8266-d1-mini-relais-als-homekit-schalter-ohne-bridge-tutorial/24).

------

Suchst du gezielt nach weiteren Tutorials mit dieser Library? Dann schau doch mal in der Community unter dem Tag [homekit-esp8266](https://web.archive.org/web/20220627125251/https://my.makesmart.net/tags/homekit-esp8266) vorbei.

*Disclaimer: Ich bin gerade frisch √ºber diese Libary gesto√üen und sehe darin (f√ºr mich pers√∂nlich) sehr viel potenzial. Ich werde vermutlich noch weitere Tutorials √ºber die Libary und das Thema allgemein ver√∂ffentlichen. Es soll nicht bei diesem einen Schalter bleiben. Thermostate, Sensoren und so weiter sind ebenfalls m√∂glich.*

\#D1-Mini #ESP8266 #HomeKit #Smarthome